apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'com.raza.deviceagent'
        minSdkVersion 14
        targetSdkVersion 23

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    dexOptions {
        incremental false
        javaMaxHeapSize "2048M"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        normal {
            buildConfigField "boolean", "USE_LEAK_CANARY", "true"
        }
        noLeakCanary {
            buildConfigField "boolean", "USE_LEAK_CANARY", "false"
        }
    }

    packagingOptions {
        exclude '.readme'
    }

    // until all third party packages got updated
    useLibrary 'org.apache.http.legacy'

}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
    compile project(':modules:BottomSheet:library')
    compile(project(':modules:ExecutionLibrary:library')) {
        exclude group: 'com.github.Evisceration', module: 'logger'
    }
    compile(project(':modules:HardwareLibrary:library')) {
        exclude group: 'com.github.Evisceration', module: 'logger'
    }
    // see https://github.com/Evisceration/logger
    compile project(':modules:logger:library')
    // see https://github.com/Evisceration/MaterialPreferences
    compile project(':modules:MaterialPreferences:library')
    // Debugging, see https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:support-v13:23.0.0'
    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
    // see https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.3'
    // see https://github.com/koush/ion
    compile 'com.koushikdutta.ion:ion:2.1.6'
    // see https://www.pollfish.com/android
    compile 'com.pollfish:pollfish:4.1.0:googleplayRelease@aar'
    compile 'com.google.android.gms:play-services-ads:7.8.0'
    // see https://github.com/pilgr/Paper
    //compile 'io.paperdb:paperdb:0.9'
    compile 'com.github.Evisceration:Paper:6ad14da863'
}
